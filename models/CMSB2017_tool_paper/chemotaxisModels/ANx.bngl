## ANx.bngl - the extended version of the Assistance neighborhood model that includes
## an updated receptor toplogy.  The original AN model (presented in Hansen, Endres,
## & Wingreen, PLOS Comp.Bio,4,(1) 2008.)
##
##
## written by Michael Sneddon


begin parameters

    # CheR/CheB rates for binding and unbinding the tether.  In the original
    # AN model, binding rates are in units of per second (because CheR/CheB are
    # treated as states of the receptor).  These rates are not valid here, so
    # instead we use the binding rate from AgentCell for CheR. The original 
    # AN model used the same binding rate for CheR and CheB, so we do the 
    # same thing here.  The unbinding rate is unchanged from the original model.
    #         Original study, konR=konB=0.01/s
    #         AgentCell (Emonet, et. al, Bioinfo.2005 21(11):2714-2721;)
    Vol 1.41e-15 #liters
 	Na 6.02e23
 	konR 2.8e6/(Na*Vol) #2.8uM
 	konB 2.8e6/(Na*Vol)
	koffR 0.1 #s^-1
	koffB 0.1 #s^-1
	
	# Methylation and demethylation catalytic rates, taken directly from
    # the original Assistance Neighborhood model
	kr 0.1
	kb 0.2
	

	# Free energy of dimers for different methylation levels, also defined in
	# in original study.
	e0 1.0
	e1 0.5
	e2 0.0
	e3 -0.3
	e4 -0.6
	e5 -0.85
	e6 -1.1
	e7 -2.0
	e8 -3.0
	
	
	#Binding constants of TAR and TSR to attractant Aspartate, as
    #defined in the original model.
	Asp_Koff_TAR  0.02 #mM
	Asp_Kon_TAR   0.5  #mM
	Asp_Koff_TSR  100  #mM
	Asp_Kon_TSR   10^6 #mM
	
	#Tar and Tsr per cluster
	TAR_COUNT 6
	TSR_COUNT 12
	
	#Initial aspartate concentration
	AspConc 0 #mM

	# Number of molecules we want to simulate.  Assuming a cell has ~14,000 
    # chemoreceptors, that gives us ~7000 receptors dimers, and 370 clusters 
    # of 19 receptor dimers.  The numbers of CheR and CheB are high, but are 
    # set to give a timescale of adaptation similar to the original model.  In 
    # a real cell, CheR and CheB will be less (on the order of ~200), but
	# the catalytic rate of methylation/demethylation will likely be faster.
    # see Li Hazelbauer, J. of Bact.(2004),Vol.186, No.12, p.3687-3694 for
    # better approximations of the number of molecules in a real cell
	ClusterCount 370
    CheRCount 500
	CheBCount 500

end parameters



begin species

    #  Cluster Description, and initialization
	#
	#  This is how the overall structure of the signaling team is set up, with each
	#  number here cooresponding to a trimer of dimers.
	#
	#    0 - 1
	#  /       \
	# 5         2
	#  \       /
	#    4 - 3
	#
	# Here we have all the bonds listed, with double dashes indicating the hex bond
	# and single dashes representing the tri site
    #
    #         T        T
    #        / \      / \
    #       T - T -- T - T
    #      /              \
    #     /                \
    #    T                  T
    #T < |                  | > T
    #    T                  T
    #     \                /
    #       T - T -- T - T
    #        \ /      \ /
    #         T        T
	#RD(RB,a,tri1!10,tri2!30,hex!1,m~2).RD(RB,a,tri1!20,tri2!10,hex!6,m~2).RD(RB,a,tri1!30,tri2!20,hex,m~2).
	#RD(RB,a,tri1!11,tri2!31,hex!1,m~2).RD(RB,a,tri1!21,tri2!11,hex!2,m~2).RD(RB,a,tri1!31,tri2!21,hex,m~2).
	#RD(RB,a,tri1!12,tri2!32,hex!3,m~2).RD(RB,a,tri1!22,tri2!12,hex!2,m~2).RD(RB,a,tri1!32,tri2!22,hex,m~2).
	#RD(RB,a,tri1!13,tri2!33,hex!3,m~2).RD(RB,a,tri1!23,tri2!13,hex!4,m~2).RD(RB,a,tri1!33,tri2!23,hex,m~2).
	#RD(RB,a,tri1!14,tri2!34,hex!5,m~2).RD(RB,a,tri1!24,tri2!14,hex!4,m~2).RD(RB,a,tri1!34,tri2!24,hex,m~2).
	#RD(RB,a,tri1!15,tri2!35,hex!5,m~2).RD(RB,a,tri1!25,tri2!15,hex!6,m~2).RD(RB,a,tri1!35,tri2!25,hex,m~2) ClusterCount


	RD(RB,a,tri1!10,tri2!30,hex!1,m~2).RD(RB,a,tri1!20,tri2!10,hex!6,m~2).RD(RB,a,tri1!30,tri2!20,hex,m~2).RD(RB,a,tri1!11,tri2!31,hex!1,m~2).RD(RB,a,tri1!21,tri2!11,hex!2,m~2).RD(RB,a,tri1!31,tri2!21,hex,m~2).RD(RB,a,tri1!12,tri2!32,hex!3,m~2).RD(RB,a,tri1!22,tri2!12,hex!2,m~2).RD(RB,a,tri1!32,tri2!22,hex,m~2).RD(RB,a,tri1!13,tri2!33,hex!3,m~2).RD(RB,a,tri1!23,tri2!13,hex!4,m~2).RD(RB,a,tri1!33,tri2!23,hex,m~2).RD(RB,a,tri1!14,tri2!34,hex!5,m~2).RD(RB,a,tri1!24,tri2!14,hex!4,m~2).RD(RB,a,tri1!34,tri2!24,hex,m~2).RD(RB,a,tri1!15,tri2!35,hex!5,m~2).RD(RB,a,tri1!25,tri2!15,hex!6,m~2).RD(RB,a,tri1!35,tri2!25,hex,m~2) ClusterCount

	CheR(tether,active) CheRCount
	CheB(tether,active) CheBCount

end species




begin observables

	# Observables to keep track of the methylation level
	# of individual receptors and receptors in the cluster
	Molecules R0 RD(m~0)
	Molecules R1 RD(m~1)
	Molecules R2 RD(m~2)
	Molecules R3 RD(m~3)
	Molecules R4 RD(m~4)
	Molecules R5 RD(m~5)
	Molecules R6 RD(m~6)
	Molecules R7 RD(m~7)
	Molecules R8 RD(m~8)
	Molecules RDtot RD()

	# Other observables that track the number of R/B bound can
	# be defined here
	Molecules RD_R RD(RB!1).CheR(tether!1)
	Molecules RD_B RD(RB!1).CheB(tether!1)

end observables



begin functions

	# Keeps track of the methylation level for a single dimer, so
	# that the rates of methylation and demethylation can vary according
	# to the number of available methylation sites
	MethLevel(x) = 1*R1(x)+2*R2(x)+3*R3(x)+4*R4(x)+5*R5(x)+6*R6(x)+7*R7(x)+8*R8(x)

	# activity function, which is the probability for the kinase to be on, 
	# defined here as in the original study
	pOn(x) = 1/(1+(exp(e0*R0(x)+e1*R1(x)+e2*R2(x)+e3*R3(x)+e4*R4(x)+e5*R5(x)+e6*R6(x)+e7*R7(x)+e8*R8(x))*((1.0+(AspConc/Asp_Koff_TAR))/(1.0+(AspConc/Asp_Kon_TAR)))^TAR_COUNT*((1.0+(AspConc/Asp_Koff_TSR)) / (1.0+(AspConc/Asp_Kon_TSR)))^TSR_COUNT))

	#simplified activity function, for reference:
	#pOn = 1.0 / (1.0 + (exp(FreeEnergyOffsetSum) * TAR_LOG_TERM * TSR_LOG_TERM));

end functions




begin reaction rules
	
	##  R/B binding and unbinding to a receptor tether, only one R or B is
	##  allowed to bind per dimer, the active site must also be free
	RD(RB) + CheR(tether,active) -> CheR(tether!1,active).RD(RB!1) konR
	RD(RB) + CheB(tether,active) -> CheB(tether!1,active).RD(RB!1) konB
	
	## Unbinding from the tether, does not depend on active site
	CheR(tether!1).RD(RB!1) -> CheR(tether) + RD(RB) koffR
	CheB(tether!1).RD(RB!1) -> CheB(tether) + RD(RB) koffB
	
	## If R or B unbinds a tether, it rapidly falls off the receptor without
	## methylating or demethylating, which fits with the original model
	CheR(tether,active!1).RD(a!1) -> CheR(tether,active) + RD(a)  1000
	CheB(tether,active!1).RD(a!1) -> CheB(tether,active) + RD(a)  1000 
	
	
	## R/B binding to active site.  Assuming saturated kinetics means 
    ## that this happens very fast, but it will still depend on the number 
    ## of sites available so that the methylation distribution is correct.
	## Binding does not depend on activity (according to the original study), 
    ## only the rate limiting catalytic step does.
	RD%y(RB!1,a).CheR(tether!1,active) -> RD%y(RB!1,a!2).CheR(tether!1,active!2) 1000*(8-MethLevel(y))
	RD%y(RB!1,a).CheB(tether!1,active) -> RD%y(RB!1,a!2).CheB(tether!1,active!2) 1000*(MethLevel(y))
	
	## Neighborhood reactions for CheR
	RD%r(tri2!1,m~?,a).RD(RB!2,tri1!1).CheR(tether!2,active) -> RD%r(tri2!1,m~?,a!3).RD(RB!2,tri1!1).CheR(tether!2,active!3) 1000*(8-MethLevel(r))
	RD%r(tri1!1,m~?,a).RD(RB!2,tri2!1).CheR(tether!2,active) -> RD%r(tri1!1,m~?,a!3).RD(RB!2,tri2!1).CheR(tether!2,active!3) 1000*(8-MethLevel(r))
	RD%r(hex!1,m~?,a).RD(RB!2,hex!1).CheR(tether!2,active)   -> RD%r(hex!1,m~?,a!3).RD(RB!2,hex!1).CheR(tether!2,active!3)   1000*(8-MethLevel(r))
	
	# Neighborhood reactions for CheB
	RD%r(tri2!1,m~?,a).RD(RB!2,tri1!1).CheB(tether!2,active) -> RD%r(tri2!1,m~?,a!3).RD(RB!2,tri1!1).CheB(tether!2,active!3) 1000*(MethLevel(r))
	RD%r(tri1!1,m~?,a).RD(RB!2,tri2!1).CheB(tether!2,active) -> RD%r(tri1!1,m~?,a!3).RD(RB!2,tri2!1).CheB(tether!2,active!3) 1000*(MethLevel(r))
	RD%r(hex!1,m~?,a).RD(RB!2,hex!1).CheB(tether!2,active)   -> RD%r(hex!1,m~?,a!3).RD(RB!2,hex!1).CheB(tether!2,active!3)   1000*(MethLevel(r))
	
	
	## Methylation and demethylation is activity dependent, and requires an
    ## R/B bound to the catalytic active site of the receptor dimer.
	%x::RD(m~?,a!1).CheR(active!1) -> %x::RD(m~PLUS,a) + CheR(active) kr*(1-pOn(x))
	%x::RD(m~?,a!1).CheB(active!1) -> %x::RD(m~MINUS,a) + CheB(active) kb*(pOn(x))
	
	
end reaction rules






writeXML();
