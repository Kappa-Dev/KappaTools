begin parameters
Lig_tot         6.0e3
Rec_tot         4.0e2
Lyn_tot         2.8e1
Syk_tot         4.0e2

 kp1  1.328452377888242e-7
 km1  0
 kp2  2.5e-1
 km2  0
 kpL  5e-2
 kmL  20
kpLs  5e-2 
kmLs  0.12
 kpS  6e-2
 kmS  0.13
kpSs  6e-2
kmSs  0.13
 pLb  30
pLbs  100
 pLg    1
pLgs    3
 pLS   30
pLSs  100
 pSS  100
pSSs  200 
  dm   20
  dc   20
end parameters

begin species
Lig(l,l) Lig_tot
Lyn(U,SH2) Lyn_tot
Syk(tSH2,l~Y,a~Y) Syk_tot
Rec(a,b~Y,g~Y) Rec_tot
end species


begin reaction_rules
# Ligand-receptor binding      
1 Rec(a) + Lig(l,l) <-> Rec(a!1).Lig(l!1,l) kp1, km1

# Receptor-aggregation
2 Rec(a) + Lig(l,l!1) <-> Rec(a!2).Lig(l!2,l!1) kp2,km2

# Constitutive Lyn-receptor binding
3 Rec(b~Y) + Lyn(U,SH2) <-> Rec(b~Y!1).Lyn(U!1,SH2) kpL, kmL

# Transphosphorylation of beta by constitutive Lyn
4 Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~Y) -> Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~pY) pLb

# Transphosphorylation of gamma by constitutive Lyn
5 Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~Y) -> Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~pY) pLg

# Lyn-receptor binding through SH2 domain
6 Rec(b~pY) + Lyn(U,SH2) <-> Rec(b~pY!1).Lyn(U,SH2!1) kpLs, kmLs

# Transphosphorylation of beta by SH2-bound Lyn
7 Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,b~Y) -> Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,b~pY) pLbs

# Transphosphorylation of gamma by SH2-bound Lyn
8 Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~Y) -> Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~pY) pLgs

# Syk-receptor binding through tSH2 domain
9 Rec(g~pY) + Syk(tSH2) <-> Rec(g~pY!1).Syk(tSH2!1) kpS, kmS

# Transphosphorylation of Syk by constitutive Lyn
#10 Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~Y) -> \
#   Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~pY) pLS

# Transphosphorylation of Syk by SH2-bound Lyn
#11 Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~Y) -> \
#   Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~pY) pLSs

# Transphosphorylation of Syk by Syk not phosphorylated on aloop
12 Lig(l!1,l!2).Syk(tSH2!3,a~Y).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~Y) -> \
   Lig(l!1,l!2).Syk(tSH2!3,a~Y).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~pY) pSS

# Transphosphorylation of Syk by Syk phosphorylated on aloop
13 Lig(l!1,l!2).Syk(tSH2!3,a~pY).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~Y) -> \
   Lig(l!1,l!2).Syk(tSH2!3,a~pY).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~pY) pSSs

# Dephosphorylation of Rec beta
14 Rec(b~pY)-> Rec(b~Y) dm

# Dephosphorylation of Rec gamma
15 Rec(g~pY)-> Rec(g~Y) dm

# Dephosphorylation of Syk at membrane
16 Syk(tSH2!1,l~pY)-> Syk(tSH2!1,l~Y) dm
17 Syk(tSH2!1,a~pY)-> Syk(tSH2!1,a~Y) dm

# Dephosphorylation of Syk in cytosol
18 Syk(tSH2,l~pY)-> Syk(tSH2,l~Y) dc
19 Syk(tSH2,a~pY)-> Syk(tSH2,a~Y) dc

end reaction_rules

begin observables
Molecules     LynFree     Lyn(U,SH2)
Molecules     RecMon	  Rec(a) Rec(a!1).Lig(l!1,l)
Molecules     RecDim      Rec.Rec                   

#Molecules     RecPbeta    Rec(b~pY) Rec(b~pY!1)

Molecules     RecPbeta    Rec(b~pY!?)


Molecules     RecPgamma   Rec(g~pY) Rec(g~pY!1)
Molecules     RecSyk      Rec(g~pY!1).Syk(tSH2!1)
Molecules     RecSykPS    Rec(g~pY!1).Syk(tSH2!1,a~pY)

Molecules     SykTest     Syk
Molecules     LynTest     Lyn
Molecules     RecTest     Rec
end observables


#generate_network({max_iter=>100});
#generate_network({overwrite=>1});
writeXML();
