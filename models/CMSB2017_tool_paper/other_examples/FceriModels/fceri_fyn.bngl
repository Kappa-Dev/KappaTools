begin model
begin parameters
    Lig_tot  6.0e3
    Rec_tot  4.0e2
    Lyn_tot  2.8e1
    Fyn_tot  4.0e2
    Syk_tot  4.0e2

     kp1  1.32845238e-7
     km1  0
     kp2  2.5e-1
     km2  0
     kpL  5e-2
     kmL  20
    kpLs  5e-2 
    kmLs  0.12
     kpF  5e-2
     kmF  20
     kpS  6e-2
     kmS  0.13
    kpSs  6e-2
    kmSs  0.13
     pLb  30
    pLbs  100
     pLg    1
    pLgs    3
     pLS   30
    pLSs  100
     pSS  100
    pSSs  200 
     pLF   30
    pLFs  100
      dm   20
      dc   20
end parameters

begin seed species
    Lig(l,l)           Lig_tot
    Lyn(U,SH2,a~Y)     Lyn_tot
    Fyn(SH2,a~Y,t~Y)   Fyn_tot
    Syk(tSH2,l~Y,a~Y)  Syk_tot
    Rec(a,b~Y,g~Y)     Rec_tot
end seed species

begin reaction rules
    # Ligand-receptor binding      
    Rec(a) + Lig(l,l) <-> Rec(a!1).Lig(l!1,l)  kp1, km1

    # Receptor-aggregation
    Rec(a) + Lig(l,l!+) <-> Rec(a!2).Lig(l!2,l!+)  kp2,km2

    # Constitutive Lyn-receptor binding
    Rec(b~Y) + Lyn(U,SH2) <-> Rec(b~Y!1).Lyn(U!1,SH2)  kpL, kmL

    # Transphosphorylation of beta by constitutive Lyn
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~Y) -> Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~pY) pLb

    # Transphosphorylation of gamma by constitutive Lyn
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~Y) -> Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~pY) pLg

    # Lyn-receptor binding through SH2 domain
    Rec(b~pY) + Lyn(U,SH2) <-> Rec(b~pY!1).Lyn(U,SH2!1) kpLs, kmLs

    # Fyn-receptor binding through SH2 domain
    Rec(b~pY) + Fyn(SH2) <-> Rec(b~pY!1).Fyn(SH2!1) kpF, kmF

    # Transphosphorylation of beta by SH2-bound Lyn
    Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,b~Y) -> Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,b~pY) pLbs

    # Transphosphorylation of gamma by SH2-bound Lyn
    Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~Y) -> Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~pY) pLgs

    # Syk-receptor binding through tSH2 domain
    Rec(g~pY) + Syk(tSH2) <-> Rec(g~pY!1).Syk(tSH2!1) kpS, kmS

    # Transphosphorylation of Lyn by constitutive Lyn
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~pY!4).Lyn(SH2!4,a~Y) -> \
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~pY!4).Lyn(SH2!4,a~pY) pLS

    # Transphosphorylation of Lyn by SH2-bound Lyn
    Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,b~pY!4).Lyn(SH2!4,a~Y) -> \
    Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,b~pY!4).Lyn(SH2!4,a~pY) pLSs

    # Transphosphorylation of Fyn by constitutive Lyn
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~pY!4).Fyn(SH2!4,a~Y) -> \
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~pY!4).Fyn(SH2!4,a~pY) pLF

    # Transphosphorylation of Fyn by SH2-bound Lyn
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~pY!3).Rec(a!1,b~pY!4).Fyn(SH2!4,a~Y) -> \
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~pY!3).Rec(a!1,b~pY!4).Fyn(SH2!4,a~pY) pLFs

    # Transphosphorylation of Syk by constitutive Lyn
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~Y) -> \
    Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~pY) pLS

    # Transphosphorylation of Syk by SH2-bound Lyn
    Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~Y) -> \
    Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~pY) pLSs

    # Transphosphorylation of Syk by Syk not phosphorylated on aloop
    Lig(l!1,l!2).Syk(tSH2!3,a~Y).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~Y) -> \
    Lig(l!1,l!2).Syk(tSH2!3,a~Y).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~pY) pSS

    # Transphosphorylation of Syk by Syk phosphorylated on aloop
    Lig(l!1,l!2).Syk(tSH2!3,a~pY).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~Y) -> \
    Lig(l!1,l!2).Syk(tSH2!3,a~pY).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~pY) pSSs

    # Dephosphorylation of Rec beta
    Rec(b~pY)-> Rec(b~Y) dm

    # Dephosphorylation of Rec gamma
    Rec(g~pY)-> Rec(g~Y) dm

    # Dephosphorylation of Syk at membrane
    Syk(tSH2!+,l~pY)-> Syk(tSH2!+,l~Y) dm
    Syk(tSH2!+,a~pY)-> Syk(tSH2!+,a~Y) dm

    # Dephosphorylation of Syk in cytosol
    Syk(tSH2,l~pY)-> Syk(tSH2,l~Y) dc
    Syk(tSH2,a~pY)-> Syk(tSH2,a~Y) dc
end reaction rules
end model

## actions ##
generate_network({overwrite=>1,max_iter=>100})
